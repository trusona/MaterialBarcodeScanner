task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

artifactory {
    contextUrl = "https://trusona.jfrog.io/trusona/"
    publish {
        repository {
            repoKey = "sdk-snapshots-local" // exactly this
            username = System.getenv("artifactory_username")?.trim() ?: trusonaUsername
            password = System.getenv("artifactory_password")?.trim() ?: trusonaPassword
            maven = true
        }
        defaults {
            publications("aar")
            publishArtifacts = true
            publishPom = true
            publishIvy = false
        }
    }
    resolve {
        repository {
            repoKey = "sdk-snapshots-local" // exactly-this
            username = System.getenv("artifactory_username")?.trim() ?: trusonaUsername
            password = System.getenv("artifactory_password")?.trim() ?: trusonaPassword
            maven = true
        }
    }
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId "${theGroupId}"
            artifactId "${theArtifactId}"
            version "${theVersion}"
            artifact source: file("${theArtifactSource}")
            artifact source: file("${debugArtifactSource}"), classifier: "debug"

            pom.withXml {
                def dependenciesNode = asNode().appendNode("dependencies")

                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode("dependency")
                    dependencyNode.appendNode("groupId", it.group)
                    dependencyNode.appendNode("artifactId", it.name)
                    dependencyNode.appendNode("version", it.version)
                }
            }
        }
        local(MavenPublication) {
            groupId "${theGroupId}"
            artifactId "${theArtifactId}"
            version "${theVersion}"
            artifact source: file("${theArtifactSource}")
            artifact source: file("${debugArtifactSource}"), classifier: "debug"
            artifact(sourceJar)

            pom.withXml {
                def dependenciesNode = asNode().appendNode("dependencies")

                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode("dependency")
                    dependencyNode.appendNode("groupId", it.group)
                    dependencyNode.appendNode("artifactId", it.name)
                    dependencyNode.appendNode("version", it.version)
                }
            }
        }
    }
    repositories {
        mavenLocal()
    }
}